# History
setopt hist_ignore_all_dups hist_ignore_space share_history inc_append_history extended_history
setopt notify auto_cd interactive_comments prompt_subst

# Fast compinit
zmodload zsh/complist
autoload -Uz compinit

# Caching
typeset -g ZCOMPDUMP="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/zcompdump-$ZSH_VERSION"
mkdir -p "${ZCOMPDUMP:h}"

if [[ ! -r $ZCOMPDUMP ]]; then
  compinit -i -d "$ZCOMPDUMP"
else
  compinit -C -d "$ZCOMPDUMP"
fi

# Compile dump to speed future loads
if [[ -s $ZCOMPDUMP && ( ! -s $ZCOMPDUMP.zwc || $ZCOMPDUMP -nt $ZCOMPDUMP.zwc ) ]]; then
  zcompile -R -- "$ZCOMPDUMP"
fi

# Completion bindings
bindkey '^I' expand-or-complete
bindkey '^[[Z' menu-complete

autoload -Uz _ls
compdef eza=ls

# Git prompt
git_prompt() {
  git rev-parse --is-inside-work-tree >/dev/null 2>&1 || return 0

  local branch dirty
  branch=$(git symbolic-ref --quiet --short HEAD 2>/dev/null) \
    || branch=$(git rev-parse --short HEAD 2>/dev/null) \
    || return 0

  if [[ -n "$(git status --porcelain --ignore-submodules=dirty 2>/dev/null)" ]]; then
    dirty="*"
  else
    dirty=""
  fi

  printf ' %s%s %s%s' "%F{8}" "%f" "%F{13}${branch}%f" "%F{red}${dirty}%f"
}

# Prompt: user@host dir (newline) ❯
PROMPT='%F{green}%n%f@%F{blue}%m%f %F{yellow} %~%f$(git_prompt)
%F{8}❯%f '

# Aliases
if (( $+commands[eza] )); then
  alias ls='eza --icons=always --group-directories-first --classify'
  alias ll='eza -l --icons=always --group-directories-first --classify --git'
  alias la='eza -la --icons=always --group-directories-first --classify --git'
else
  alias ls='ls --color=auto -F'
  alias ll='ls -alF --color=auto'
  alias la='ls -A --color=auto'
fi